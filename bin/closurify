#!/usr/bin/env coffee
_ = require 'underscore-folly'

argv = require('optimist')
  .usage("$0")
  .options('help', boolean: 1, desc: "this help message")
  .options('u', alias: 'uglify', boolean: 1, desc: 'use uglfiy rather than closure')
  .options('d', alias: 'debug', boolean: 1, desc: 'output only debug code')
  .options('b', alias: 'beautify', boolean: 1, desc: 'beautify output code')
  .options('p', alias: 'print', boolean: 1, desc: 'print to stdout')
  .options('e', desc: 'expression against which to match browser files')

argvHelp = argv.help()
argv = argv.argv

if argv.help
  console.log argvHelp
  process.exit 0

closurify = require '../'
path = require 'path'
async = require 'async'
fs = require 'fs'

if argv.debug
  options =
    expression: "debug #{argv.e}"
else
  options =
    expression: "release #{argv.e}"
    release: if argv.uglify then 'uglify' else true

files = argv._.map (filePath) -> path.resolve filePath

async.each files, (filePath, cb) ->
  outPath = "#{path.dirname filePath}/#{path.basename filePath, path.extname(filePath)}-min.js"
  closurify [filePath], options, (err, code) ->
    if err?
      console.error "Error processing #{filePath}:", err.stack || err
      cb()
    else if code?
      # code = if argv.beautify then require('js-beautify') code.join '\n' else code.join '\n'
      code = _.mergeMappedCode code, true
      if argv.print
        process.stdout.write code
        cb()
      else
        fs.writeFile outPath, code, cb
    return
